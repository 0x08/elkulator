include(CheckCXXSourceCompiles)
include(CheckFunctionExists)

if(MSVC)
  include(${PROJECT_SOURCE_DIR}/cmake/msvc-checks.cmake)
else()
  include(${PROJECT_SOURCE_DIR}/cmake/gcc-checks.cmake)
endif()

# Determine if std::make_unique is supported
set(CMAKE_REQUIRED_FLAGS ${CMAKE_CXX_FLAGS})
check_cxx_source_compiles("#include <memory>\nclass C { };\nint main() { std::make_unique<C>(); }" HAVE_MAKE_UNIQUE)

# _strncasecmp and _strcasecmp
check_function_exists(strncasecmp HAVE_STRNCASECMP)
if(NOT HAVE_STRNCASECMP)
  check_function_exists(_strncasecmp HAVE__STRNCASECMP)
  if(NOT HAVE__STRNCASECMP)
    check_function_exists(_strnicmp HAVE__STRNICMP)
    if(NOT HAVE__STRNICMP)
      message(FATAL_ERROR "No _strncasecmp or _strnicmp function found")
    endif()
    add_definitions(-Dstrncasecmp=_strnicmp)
  endif()
endif()
check_function_exists(strcasecmp HAVE_STRCASECMP)
if(NOT HAVE_STRCASECMP)
  check_function_exists(_strcasecmp HAVE__STRCASECMP)
  if(NOT HAVE__STRCASECMP)
    check_function_exists(_stricmp HAVE__STRICMP)
    if(NOT HAVE__STRICMP)
      message(FATAL_ERROR "No _strcasecmp or _stricmp function found")
    endif()
    add_definitions(-Dstrcasecmp=_stricmp)
  endif()
endif()
